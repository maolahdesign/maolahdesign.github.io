# Excel 函數基礎與進階

## 基礎函數

### 函數的基本概念與應用

- 甚麼是函數

  函數是一個預先設定好的「捷徑」或「工具」，用來幫你完成特定的計算或操作，省去手動處理的麻煩。

- 為什麼需要函數？

  因為非常有效率！

  * 想加總一百個數字？
    * 不需要手動 =A1+A2+A3+…
    * 只要使用 SUM 函數：=SUM(A1:A100)
  * 想找出全班最高的成績？
    * 不需要用眼睛一個一個看。
    * 只要使用 MAX 函數：=MAX(C2:C50)
  * 想知道今天幾月幾號？
    * 不需要自己手動輸入。
    * 只要使用 TODAY 函數：=TODAY()

- 函數語法結構：`=函數名稱(參數1, 參數2, ...)`

  例：`=SUM(A1:A10) → 將 A1 到 A10 的數值加總。`

---

#### 相對引用與絕對引用

**相對**：一定要有參照對象或比較基準，結果會因為參照不同而改變。
**絕對**：不用依賴任何參照、標準、對象；本身就是如此，而且對任何人、任何時候都是一樣的。

**生活實例**

|      | 舉例                              | 說明                               |
| ---- | --------------------------------- | ---------------------------------- |
| 絕對 | 水的沸點是100°C（在標準大氣壓下） | 無論你是誰、在哪裡，標準都不會改變 |
| 相對 | 這杯咖啡比那杯熱                  | 需要跟另一杯做比較                 |
| 絕對 | 一小時有 60 分鐘                  | 全世界都一樣，標準固定             |
| 相對 | 我比你高                          | 一定要有「你」當對象，才能成立     |

**科學與數學應用**

- 絕對溫度（如開氏溫標，起點為「絕對零度」）
- 相對溫度（今天比昨天溫度高3度，「比誰」很重要）
- 絕對值（數學上 $$|-5|=5$$，不考慮方向）
- 相對差異（A商品比B商品貴10%）

**參考實作**

* **相對引用 (A1)**：
  公式複製到其他儲存格時，參照的行列會隨著位置改變。例如在 B2 輸入 =A1+10，複製到 B3 會自動變成 =A2+10。
* **絕對引用 ($A$1)**：
  公式複製到其他儲存格時，參照的行列固定不變。例如 =$A$1+10 無論複製到哪裡都仍然參照 A1。
* **混合引用**：
  * $A1 → 鎖定欄，行隨位置改變
  * A$1 → 鎖定行，欄隨位置改變
  * **說明**：使用相對與絕對引用可以控制公式在複製時的參照行列，方便做批量計算或動態範圍。

**認識錯誤訊息**：

* `#DIV/0!`：當除數為 0 時出現，例如 =10/0。
* `#N/A`：表示沒有可用值或找不到匹配，例如 =VLOOKUP(5, A1:B3, 2, FALSE) 查無結果。
* `#VALUE!`：當數值或資料型態錯誤時，例如 ="文字"+5。

>實作練習：


---

### 常用函數介紹 

#### 運算函數

##### SUM 加總

* 定義：對指定範圍或數值加總。
* 使用語法：SUM(數值1, [數值2], ...)
* 實際範例：`=SUM(A1:A10)`。

>實作練習：計算銷售紀錄表總銷售額

##### SUMIF

* 定義：根據**單一條件**加總。
* 使用語法：SUMIF( 範圍,  條件,  [加總範圍] )  如果省略**加總範圍**，會加總**範圍**指定的儲存格。
* 實際範例：

1. 條件為「文字」

   最常見的用法，用來加總符合特定文字的項目。

    * 情境：計算所有「蘋果」的總銷售額。
    * 公式：`=SUMIF(A2:A9, "蘋果", C2:C9)`
    * 說明：
      * **範圍**是 A2:A9 (產品欄)。
      * **條件**是 "蘋果"。
      * **加總範圍**是 C2:C9 (銷售額欄)。
      * 公式會檢查 A 欄中所有值為 "蘋果" 的儲存格，並將其對應的 C 欄中的數值相加。
    * 結果：220

    >實作練習：計算員工資料表工程部員工總薪資

    ```excel
   =SUMIF(B2:B191,"工程部",E2:E191)
   or
   =SUMIF(B:B,"工程部",E:E)
    ```

    如何驗證錯誤?

2. 條件為「數字運算式」

   您可以使用大於 >、小於 <、等於 = 等運算子。

   * 情境：計算單筆銷售額「大於 150」的總金額。
   * 公式：`=SUMIF(C2:C9, ">150")`
   * 說明：
     * range 是 C2:C9。
     * criteria 是 ">150" (條件必須用雙引號括起來)。
     * 因為要判斷的範圍和要加總的範圍相同，所以 [sum_range] 參數可以省略。
     * 公式會加總 C 欄中所有大於 150 的數值 (200 和 250)。
   * 結果：450

3. 條件包含「萬用字元」

   您可以使用 * (星號，代表任意多個字元) 或 ? (問號，代表單一字元) 來進行模糊比對。
   注意:萬用字元 * 和 ? 只能在「文字」上運作，無法對「數值」進行模式比對

   * 情境：計算所有名稱中含有「筆」的產品的總銷售額。
   * 公式：`=SUMIF(A2:A9, "*筆", C2:C9)`
   * 說明：
     * criteria 是 "*筆"，這會匹配任何以 "筆" 結尾的文字，例如 "鉛筆" 和 "原子筆"。
     * 公式會將其對應的銷售額 (80 和 120) 相加。
   * 結果：200

   >實作練習：計算員工資料表部門名稱有"務"的部門員工總薪資

   ```excel
   =SUMIF(B:B, "*務?", E:E)
   ```

4. 條件為「不等於」

   使用 <> 運算子來排除特定項目。

   * 情境：計算除了「北區」以外，所有地區的總銷售額。
   * 公式：`=SUMIF(B2:B9, "<>北區", C2:C9)`
   * 說明：
   * criteria 是 "<>北區"，代表不等於 "北區"。
   * 此公式會加總所有地區不是 "北區" 的銷售額 (中區的 200 和 120，以及南區的 150)。
   * 結果：470

   >實作練習： 在員工資料表計算工程部員工總薪資

   ```excel
   =SUMIF(B2:B191,"工程部",E2:E191)
   or
   =SUMIF(B:B,"工程部",E:E)
   ```

##### SUMIFS

* 定義：根據**多條件**加總。
* 使用語法：SUMIFS(加總範圍, 條件範圍1, 條件1, ...)
* 實際範例：

 1. 兩個「文字」條件

    這是 SUMIFS 最典型的用法，結合兩個或多個欄位的文字條件。

    * 情境：計算「蘋果」在「北區」的總銷售額。
    * 公式：`=SUMIFS(C2:C9, A2:A9, "蘋果", B2:B9, "北區")`
    * 說明：
    * sum_range 是 C2:C9 (要加總銷售額)。
    * 第一個條件：A2:A9 (產品) 必須是 "蘋果"。
    * 第二個條件：B2:B9 (地區) 必須是 "北區"。
    * 只有第 2 列的資料 (蘋果, 北區, 100) 同時滿足這兩個條件。
    * 結果：100

2. 「文字」與「數字」條件組合

   您可以混合使用不同類型的條件。

    * 情境：計算「橘子」這個產品中，單筆銷售額「大於 200」的總金額。
    * 公式：`=SUMIFS(C2:C9, A2:A9, "橘子", C2:C9, ">200")`
    * 說明：
      * 加總範圍是 C2:C9。
      * 第一個條件：A2:A9 (產品) 必須是 "橘子"。
      * 第二個條件：C2:C9 (銷售額) 必須 ">200"。
      * 在所有 "橘子" 的紀錄中 (第 3 列和第 6 列)，只有第 6 列的銷售額 250 滿足 >200 的條件。
    * 結果：250

3. 多重條件與「不等於」

   結合多個條件，其中一個使用排除法。

   * 情境：計算「北區」所有「非蘋果」產品的總銷售額。
   * 公式：`=SUMIFS(C2:C9, B2:B9, "北區", A2:A9, "<>蘋果")`
   * 說明：
     * sum_range 是 C2:C7。
     * 第一個條件：B2:B9 (地區) 必須是 "北區"。
     * 第二個條件：A2:A9 (產品) 必須不等於 "<>" "蘋果"。
     * 首先篩選出北區的紀錄 (第 2, 5, 6 列)，然後在這些紀錄中，排除產品為 "蘋果" 的紀錄 (排除第 2 列)。最後加總剩下紀錄 (第 5 列和第 6 列) 的銷售額 (80 + 250)。
   * 結果：330

>實作練習：計算農產品交易紀錄台北一市場橄欖及栗子的總交易量

```excel
=SUMIFS(K:K,F:F,"台北一",D:D,"橄欖")+SUMIFS(K:K,F:F,"台北一",D:D,"栗子")
```

> **建議 :** 即使只有一個條件，也推薦使用 SUMIFS。因為它的語法結構更一致(加總範圍總是在最前面)，未來若需要增加篩選條件，可以直接在公式後面添加，而不需要將整個公式從 SUMIF 修改為 SUMIFS。

##### COUNT 統計

* 定義：只計算範圍中**包含「數值」的儲存格**。
* 使用語法：COUNT(範圍)
* 說明：它會忽略文字、錯誤值和空白儲存格。非常適合用來計算有多少筆**數字資料或日期**。
* 範例：`=COUNT(A1:A10)`

##### COUNTA

* 定義：計算範圍中**「非空白」的儲存格** (A 代表 All)。
* 使用語法：COUNTA(範圍)
* 說明：無論儲存格內容是數值、文字、日期、還是錯誤值 (#N/A)，**只要不是空的，COUNTA 都會計算進去**。常用來計算總共有多少筆「有資料」的紀錄。
* 範例：`=COUNTA(A1:A10)`

##### COUNTIF

* 定義：計算在一個範圍中，符合**「單一條件」**的儲存格數量。
* 使用語法：COUNTIF(範圍, 條件)
* 實際範例 1 (文字條件)：`=COUNTIF(B1:B20, "業務部")`
  * 說明：計算 A1:A50 範圍中，內容剛好是「業務部」的儲存格有幾個。
* 實際範例 2 (數字條件)：`=COUNTIF(B1:B50, ">10000")`
  * 說明：計算 B1:B50 範圍中，數值大於 10000 的儲存格有幾個。

##### COUNTIFS

* 定義：計算在多個範圍中，「同時」符合所有指定條件的儲存格數量。所有條件之間是「而且 (AND)」的關係。
* 使用語法：COUNTIFS(條件範圍1, 條件1, [條件範圍2, 條件2], ...)
* 實際範例：`=COUNTIFS(A1:A50, "業務部", C1:C50, "經理")`
  * 說明：計算所有符合以下兩個條件的紀錄總數：
    1. 在 A1:A50 範圍中，部門必須是「業務部」。
    2. 而且在 C1:C50 範圍中，其對應的職級必須是「經理」。

>實作練習：計算農產品交易紀錄三重區椰子的交易紀錄有幾筆

```excel
=COUNTIFS(F:F,"三重區",D:D,"椰子")
```

##### ROUND 四捨五入

* 定義：**四捨五入**數值。
* 使用語法：ROUND(數值, 位數)
* 實際範例：=ROUND(3.14159,2) → 3.14。

##### ROUNDUP 無條件進位

* 定義：**無條件**進位。
* 使用語法：ROUNDUP(數值, 位數)
* 實際範例：=ROUNDUP(3.14159,2) → 3.15。

##### ROUNDDOWN 無條件捨去

* 定義：無條件捨去。
* 使用語法：ROUNDDOWN(數值, 位數)
* 實際範例：=ROUNDDOWN(3.14159,2) → 3.14。

##### ABS 絕對值

* 定義：計算絕對值。
* 使用語法：ABS(數值)
* 實際範例：

1. 基本用法

   這是最直接的應用，了解 ABS 如何處理正數、負數和零。

   * 情境：直接轉換數字。
   * 公式與結果：
     * =ABS(7)
       * 結果：7 (正數維持不變)
     * =ABS(-7)
       * 結果：7 (負數的負號被移除)
     * =ABS(0)
       * 結果：0 (零維持不變)

2. 計算兩個數值的「差距」或「差異」

   在很多情況下，我們只關心兩個數字之間差了多少，而不在乎哪個數字比較大。

   * 情境：比較「預計銷售額」與「實際銷售額」，無論達標或未達標，只想知道差額是多少。
     * A2 儲存格 (預計)：5000
     * B2 儲存格 (實際)：4800
   * 公式：=ABS(B2 - A2)
   * 說明：
     * B2 - A2 的結果是 4800 - 5000 = -200。
     * ABS(-200) 會傳回 200。
     * 如果實際銷售額 (B2) 是 5300，B2 - A2 的結果是 300，ABS(300) 同樣傳回 300。
     * 這樣您就可以得到一個永遠為正數的差異值，方便後續計算或分析。

3. 應用於條件判斷 (IF 函式)

   ABS 常用於設定一個「容錯範圍」。

   * 情境：在品質檢測中，一個零件的標準長度是 10cm (在 A2)。只要測量出來的長度 (在 B2) 與標準長度的誤差在 0.05cm
     以內，就視為合格。
    * 公式：=IF(ABS(B2 - A2) <= 0.05, "合格", "不合格")
   * 說明：
     * 假設測量值 B2 是 10.03，ABS(10.03 - 10) 的結果為 0.03。因為 0.03 <= 0.05，所以公式會傳回 "合格"。
     * 假設測量值 B2 是 9.96，ABS(9.96 - 10) 的結果為 0.04。因為 0.04 <= 0.05，所以公式會傳回 "合格"。
     * 假設測量值 B2 是 9.94，ABS(9.94 - 10) 的結果為 0.06。因為 0.06 > 0.05，所以公式會傳回 "不合格"。
   * ABS 在這裡確保了無論測量值是偏大還是偏小，我們比較的都是誤差的絕對大小。

>實作練習：計算農產品交易紀錄平均價與中價物差大於 2 顯示狀態異常，否則為正常

```excel    
=IF(ABS(J2-H2)>=2,"異常","正常")
```

##### MOD 取餘數

* 定義：取餘數。
* 使用語法：MOD(數值, 除數)
* 實際範例：=MOD(10,3) → 1。

1. 判斷奇數或偶數

   這是 MOD 最經典的用法。任何數字除以 2，餘數若為 0 則是偶數，若為 1 則是奇數。

   - 情境：檢查 A2 儲存格的數字是奇數還是偶數。
   - 公式：=IF(MOD(A2, 2) = 0, "偶數", "奇數")
   - 說明：如果 A2 的值是 10，MOD(10, 2) 結果為 0，所以公式會傳回 "偶數"。

2. 製作間隔突顯的報表 (條件式格式化)

   當您希望報表中每隔幾列就標上不同顏色，讓報表更易讀時，MOD 就非常有用。

   - 情境：希望將表格中每三列就標示為淺灰色。
   - 步驟：
     1. 選取您想格式化的範圍 (例如：A2:E50)。
     2. 到「常用」->「條件式格式化」->「新增規則」。
     3. 選擇「使用公式來決定要格式化哪些儲存格」。
     4. 在公式欄位輸入：=MOD(ROW(), 3) = 0
     5. 點擊「格式」，設定您想要的背景色彩 (如淺灰色)。
   - 說明：ROW() 會傳回儲存格所在的列號。MOD(ROW(), 3) = 0 的意思是「當列數能被 3 整除時」，就套用此格式。

3. 分組或循環分配

   當您需要將一組項目循環分配給幾個人或幾個組別時，MOD 可以輕鬆完成。

   - 情境：您有一份名單 (在 A 欄)，需要將他們依序分配到 "甲組"、"乙組"、"丙組"。
   - 公式：=CHOOSE(MOD(ROW(A1)-1, 3) + 1, "甲組", "乙組", "丙組")
   - 說明：
     - ROW(A1)-1 會產生一個從 0 開始的序列 (0, 1, 2, 3, ...)。
     - MOD(..., 3) 會讓這個序列變成 (0, 1, 2, 0, 1, 2, ...)。
     - +1 讓序列變成 (1, 2, 3, 1, 2, 3, ...)。
     - CHOOSE 函式會根據第一個數字，選擇後面第 N 個值。所以當數字是 1 時選 "甲組"，2 時選 "乙組"，依此類推。

##### MAX 最大值

* 定義：求最大值。
* 使用語法：MAX(範圍)
* 實際範例：=MAX(A1:A10)。

>練習:  在員工資料表找出公司最高薪資是多少。

##### MIN 最小值

* 定義：求最小值。
* 使用語法：MIN(範圍)
* 實際範例：=MIN(A1:A10)。

>練習:  在員工資料表找出公司最低薪資。

##### AVERAGE 平均值

* 定義：求平均值。
* 使用語法：AVERAGE(範圍)
* 實際範例：=AVERAGE(A1:A10)。

>練習:  在員工資料表計算公司的平均薪資。

##### AVERAGEIFS 

* 定義：計算一個範圍內，符合所有指定條件 (AND 邏輯) 的儲存格的平均值。
* 使用語法：AVERAGEIFS(計算平均的儲存格範圍, 第一個要評估條件的儲存格範圍, 第一個評估條件, [第二個要評估條件的儲存格範圍, 第二個評估條件], ...)
* 實際範例：


1. 兩個「文字」條件

   計算同時滿足兩個文字條件的平均值。

   * 情境：計算「蘋果」在「北區」的平均銷售額。
   * 公式：`=AVERAGEIFS(C2:C9, A2:A9, "蘋果", B2:B9, "北區")`
   * 說明：
     * 範圍：C2:C9 (銷售額)。
     * 條件1：A2:A9 (產品) 必須是 "蘋果"。
     * 條件2：B2:B9 (地區) 必須是 "北區"。
     * 公式會找出 A 欄為 "蘋果" 且 B 欄為 "北區" 的所有紀錄，然後計算其對應 C 欄的平均值。

2. 「文字」與「數字」條件組合

   混合使用不同類型的條件。

   * 情境：計算「橘子」這個產品中，單筆銷售額「大於或等於 200」的平均金額。
   * 公式：`=AVERAGEIFS(C2:C9, A2:A9, "橘子", C2:C9, ">=100")`
   * 說明：
     * 找出 A 欄為 "橘子" 且 C 欄的數值 >=100 的所有紀錄。
     * 計算其對應銷售額的平均值。

3. 三個條件

   您可以堆疊多個條件來進行更精確的篩選。

   * 情境：計算「二月」份在「北區」所有「蘋果」產品的平均銷售額。
   * 公式：`=AVERAGEIFS(D2:D7, A2:A7, "二月", C2:C7, "北區", B2:B7, "蘋果")`
   * 說明：
     * 找出 A 欄為 "二月" 且 C 欄為 "北區" 且 B 欄為 "蘋果" 的紀錄。
     * 只有第 7 列 (二月, 蘋果, 北區, 180) 符合所有條件。
   * 計算：因為只有一筆符合，所以平均值就是其本身。

4. 使用「儲存格參照」作為條件 (最佳實踐)

   將條件寫在儲存格中，可以讓您的報表更有彈性，無需每次都修改公式。

   * 情境：假設您在 F2 儲存格輸入想查詢的「產品」，在 G2 儲存格輸入想查詢的「地區」。
     * F2 儲存格內容：蘋果
     * G2 儲存格內容：北區
   * 公式：=AVERAGEIFS(D2:D7, B2:B7, F2, C2:C7, G2)
   * 說明：公式中的條件直接參照 F2 和 G2 儲存格。當您改變 F2 或 G2 的內容 (例如改成
     "橘子")，公式結果會自動更新，非常靈活。
   * 結果：140 (與範例一相同，但更具彈性)

#### 日期與時間函數

##### YEAR 年

* 定義：取年份。
* 使用語法：YEAR(日期)
* 實際範例：`=YEAR("2025/10/02")` → 2025。

##### MONTH 月

* 定義：取月份。
* 使用語法：MONTH(日期)
* 實際範例：`=MONTH("2025/10/02")` → 10。

##### DAY 日

* 定義：取日。
* 使用語法：DAY(日期)
* 實際範例：`=DAY("2025/10/02")` → 2。

##### TODAY 今天

* 定義：傳回今天日期。
* 使用語法：TODAY()
* 實際範例：=TODAY()。

##### NOW 現在

* 定義：傳回當前日期與時間。
* 使用語法：NOW()
* 實際範例：=NOW()。

##### DATEVALUE

* 定義：將一個儲存為「文字 (Text)」格式的日期，轉換成 Excel 可辨識的「序列值 (Serial Number)」，這樣才能進行後續的日期計算或排序。
* 使用語法：DATEVALUE(文字日期)
* 實際範例：=DATEVALUE("2025/10/02")。

1. 基本轉換與格式設定

   這是最基本的用法，將一個標準的日期文字轉換成序列值。

   * 情境：將儲存格 A2 中的文字 "2025/10/15" 轉換為日期序列值。
   * 公式：=DATEVALUE(A2) 或 =DATEVALUE("2025/10/15")
   * 結果與說明：
     * 如果您將公式所在儲存格的格式設定為「通用格式」或「數值」，您會看到結果是 `45944`。這就是 2025/10/15
       的日期序列值。
     * 如果您將該儲存格的格式設定為「日期」，您才會看到熟悉的 `2025/10/15`。
     * 重點：DATEVALUE 產出的是數字，我們看到的外觀取決於儲存格的格式設定。

2. 處理不同的文字格式

   DATEVALUE 很聰明，可以辨識多種常見的日期文字格式 (辨識能力可能受您電腦的地區設定影響)。

   * 情境：您的資料來源有多種不同的日期文字格式。
   * 公式與結果 (均為序列值 `45944`)：
     * =DATEVALUE("15-Oct-2025")
     * =DATEVALUE("October 15, 2025")
     * =DATEVALUE("2025年10月15日")
   * 注意：如果您的日期文字格式很特殊 (例如 15.10.2025) 且 DATEVALUE 無法辨識，您可能需要先用 LEFT, MID, RIGHT 等文字函式將年、月、日拆分出來，再用 DATE 函式 (=DATE(年, 月, 日)) 將其組合成正確的日期。

3. 實際應用 - 日期計算

   這是 DATEVALUE 最重要的用途：讓文字格式的日期可以參與計算。

   * 情境：假設 A2 儲存格有一個從系統導出的文字格式的專案開始日期
     "2025/9/1"。您想計算到今天為止，專案已經進行了幾天。
   * 錯誤的公式：=TODAY() - A2
     * 這會傳回 #VALUE! 錯誤，因為 Excel 無法從「日期序列值」(由 TODAY() 產生) 中減去一個「文字」。
   * 正確的公式：=TODAY() - DATEVALUE(A2)
   * 說明：
     1. DATEVALUE(A2) 會先將文字 "2025/9/1" 轉換成其對應的序列值 (45900)。
     2. TODAY() 會傳回今天的日期序列值 (假設今天是 2025/10/15，序列值就是 45944)。
     3. 45944 - 45900 的結果就是 44。
     4. 這表示專案已經進行了 44 天。

##### TIMEVALUE

* 定義：將文字轉為時間。
* 使用語法：TIMEVALUE(文字時間)
* 實際範例：=TIMEVALUE("08:30")。

##### WEEKDAY

* 定義：回傳星期數字。
* 使用語法：WEEKDAY(日期, [回傳類型])
* 實際範例：=WEEKDAY("2025/10/02") → 5。

##### NETWORKDAYS

* 定義：計算工作日天數。

* 使用語法：NETWORKDAYS(開始日期, 結束日期, [假日])

  >假日: 一個包含假日日期的儲存格範圍，這些日期將不會被計為工作日。

* 實際範例：

1：基本用法 (不含假日)

   * 情境：計算 2025年10月15日 (在 A2) 到 2025年10月31日 (在 B2) 之間有幾個工作日。
   * 公式：`=NETWORKDAYS(A2, B2)`
   * 結果：13
     * 在這段期間總共有 17 天，扣除了 2 個週六和 2 個週日，所以是 13 個工作日。

2：包含自訂假日

   * 情境：同上，但這次我們要額外扣除一個國慶日補假 2025年10月10日 和一個公司活動日
     2025年10月20日。我們將這兩個假日日期寫在 D2 和 D3 儲存格。
       * A2: 2025/10/1
       * B2: 2025/10/31
       * D2: 2025/10/10
       * D3: 2025/10/20
   * 公式：`=NETWORKDAYS(A2, B2, D2:D3)`
   * 結果：21
     * 在 10 月份總共有 23 個工作日 (扣除 4 個週六和 4 個週日)。現在公式會再從這 23 天中扣除 10/10 (週五) 和
       10/20 (週一) 這兩個假日，所以 23 - 2 = 21。

  進階提示：NETWORKDAYS.INTL

  如果您的週末不是週六和週日，可以使用功能更強大的 NETWORKDAYS.INTL 函式。它允許您自訂哪天是週末。

   * 範例：假設您的公司是週日和週一休息。
   * 公式：=NETWORKDAYS.INTL(A2, B2, 2)
     * 第三個參數 2 代表「週日和週一」為週末。

   **設定週末的兩種設定方式**

      1. 使用「數字代碼」(常用)
         Excel 提供了一組數字 (1-17) 來代表常見的週末組合。


|  數字代碼  |   代表的週末   |
| :--------: | :------------: |
| 1 (或省略) | 星期六、星期日 |
|     2      | 星期日、星期一 |
|     3      | 星期一、星期二 |
|     4      | 星期二、星期三 |
|     5      | 星期三、星期四 |
|     6      | 星期四、星期五 |
|     7      | 星期五、星期六 |
|     11     |   只有星期日   |
|     12     |   只有星期一   |
|     13     |   只有星期二   |
|     14     |   只有星期三   |
|     15     |   只有星期四   |
|     16     |   只有星期五   |
|     17     |   只有星期六   |

   2. 使用「文字字串」(最靈活)
      這是最強大的方式，您可以傳入一個由 7 個 0 或 1 組成的文字字串，來定義一週中的每一天是否為工作日。

       * 字串固定為 7 位，第一位代表星期一，第七位代表星期日。
       * 1 代表非工作日 (休息日)。
       * 0 代表工作日。


       字串範例：

       * "0000011"：代表週六和週日休息 (與數字代碼 1 相同)。
       * "1000100"：代表週一和週五休息。
       * "0010000"：代表只有週三休息。

        `=NETWORKDAYS.INTL(A2, B2, "0010000")`

#### 邏輯函數

##### IF 假如

* 定義：條件判斷。
* 使用語法：`IF(條件, 值_if_true, 值_if_false)`
* 實際範例：=IF(A1>=60,"及格","不及格")。

>練習: 將公司薪資 38000 以下的員工薪資調高 4%。

```excel
=IF(E2 - 38000 > 0, E2, E2*1.04)
or
=IF(E2 > 38000, E2, E2*1.04)

```

##### IFS

* 定義：多重條件判斷。
* 使用語法：IFS(條件1, 結果1, 條件2, 結果2, ...)
* 實際範例：=IFS(A1>=90,"A",A1>=80,"B",A1>=70,"C")。

##### CHOOSE

* 定義：可以根據您提供的「索引數字 (第幾個)」，從後續一連串的「選項清單」中，挑選出對應位置的數值或項目。
* 使用語法： CHOOSE(索引數字, 選項1, [選項2], ...)
  * 索引數字 (index_num)：一個 1 到 254 之間的數字，用來決定要挑選第幾個選項。
  * 選項1, 選項2, ... (value1, value2, ...)：您提供的選項清單，可以是文字、數字、儲存格參照或另一個函式。
* 實際範例：

情境：根據 A1 儲存格的排名數字 (1, 2, 或 3)，顯示對應的獎牌「金牌」、「銀牌」或「銅牌」。

公式：

```excel
=CHOOSE(A1, "金牌", "銀牌", "銅牌")
```

* 說明：
  * 如果 A1 是 1，公式回傳「金牌」。
  * 如果 A1 是 2，公式回傳「銀牌」。
  * 如果 A1 是 3，公式回傳「銅牌」。
  * 如果 A1 是 1, 2, 3 以外的數字 (例如 4) 或文字，公式會傳回 #VALUE! 錯誤。


情境：A1 儲存格有一個日期 (例如 2025/10/15)，我們想知道那天是星期幾。

公式：

```excel
=CHOOSE(WEEKDAY(E2, 2), "星期一", "星期二", "星期三", "星期四", "星期五", "星期六", "星期日")
```

* 說明：
  1. WEEKDAY(A1, 2)：這個函式會計算 A1 日期是星期幾。參數 2 代表讓「星期一」等於 1，「星期二」等於
     2，依此類推。假設 2025/10/15 是星期三，WEEKDAY 就會傳回 3。
  2. CHOOSE(...)：CHOOSE 函式接收到數字 3 後，就會從後面的清單中挑選出第 3 個選項，也就是「星期三」。

情境：我們想從「東、南、西、北」四個組別中，隨機指派一個組別給某位員工。

公式：

```excel
=CHOOSE(RANDBETWEEN(1, 4), "東區", "南區", "西區", "北區")
```

* 說明：
  1. RANDBETWEEN(1, 4)：這個函式會隨機產生一個 1 到 4 之間的整數。
  2. CHOOSE(...)：根據 RANDBETWEEN 產生的隨機數字 (例如 2)，從清單中挑選出對應的組別
     (例如「南區」)。每次重算工作表 (例如按 F9)，結果都可能會改變。

##### AND 及、和

* 定義：同時滿足多個條件。
* 使用語法：AND(條件1, 條件2, ...)
* 實際範例：`=AND(B2="工程部",E2>50000)` → TRUE 或 FALSE。

>練習: 如果業務部員工薪資低於四萬則顯示調薪，大於則保持原薪資不變。

```
=IF(AND(B2="業務部",E2<40000),"調薪",E2)
```

##### OR 或

* 定義：任一條件成立。
* 使用語法：OR(條件1, 條件2, ...)
* 實際範例：=OR(A1>90,B1="台北") → TRUE 或 FALSE。

>練習: 同上個練習，將調薪部門增加業務部

```
=IF(AND(OR(B3="客服部",B3="財務部"),E3<50000),"調薪",E3)
```

##### NOT 相反

* 定義：邏輯反轉。
* 使用語法：NOT(條件)
* 實際範例：`=NOT(A1>60)` → TRUE 或 FALSE。

>練習: 同上個練習，除了人資部所有部門皆調薪

```
=IF(AND(NOT(B2="人資部"),E2<60000),"調薪",E2)
```

##### IFERROR 假如出現錯誤

* 定義：錯誤處理。可以捕捉多種錯誤，包括 #N/A, #VALUE!, #REF!, #DIV/0!, #NUM!, #NAME? 等。
* 使用語法：IFERROR(值, 錯誤時回傳值)
* 實際範例：`=IFERROR(A2/B2,0)`
      1. Excel 嘗試計算 A2/B2。
          2. 如果 B2 不是 0：公式會回傳正常的計算結果（例如 0.25）。
          3. 如果 B2 是 0：計算會產生 #DIV/0! 錯誤。IFERROR 會捕捉到此錯誤，並回傳 0。
          4. 以農產品交易行情表說明總成交值 `=IFERROR(J2*K2,0)`

##### IFBLANK 假如是空的

* 定義：空白判斷。
* 使用語法：`=IF(ISBLANK(L2),"10/28",L2)` or `=IF(L2="","10/28",L2)`


### 函數總整理表格

| 函式名稱    | 用途             | 使用語法                                |
| ----------- | ---------------- | --------------------------------------- |
| SUM         | 數值加總         | SUM(數值1, [數值2], ... )               |
| SUMIF       | 單一條件加總     | SUMIF(範圍, 條件, [加總範圍])           |
| SUMIFS      | 多條件加總       | SUMIFS(加總範圍, 條件範圍1, 條件1, ...) |
| ROUND       | 四捨五入         | ROUND(數值, 位數)                       |
| ROUNDUP     | 無條件進位       | ROUNDUP(數值, 位數)                     |
| ROUNDDOWN   | 無條件捨去       | ROUNDDOWN(數值, 位數)                   |
| ABS         | 絕對值           | ABS(數值)                               |
| MOD         | 取餘數           | MOD(數值, 除數)                         |
| MAX         | 最大值           | MAX(範圍)                               |
| MIN         | 最小值           | MIN(範圍)                               |
| AVERAGE     | 平均值           | AVERAGE(範圍)                           |
| YEAR        | 取年份           | YEAR(日期)                              |
| MONTH       | 取月份           | MONTH(日期)                             |
| DAY         | 取日             | DAY(日期)                               |
| TODAY       | 當天日期         | TODAY()                                 |
| NOW         | 當前日期與時間   | NOW()                                   |
| DATEVALUE   | 文字轉日期       | DATEVALUE(文字日期)                     |
| TIMEVALUE   | 文字轉時間       | TIMEVALUE(文字時間)                     |
| WEEKDAY     | 回傳星期數字     | WEEKDAY(日期,[回傳類型])                |
| NETWORKDAYS | 計算工作日天數   | NETWORKDAYS(開始日期, 結束日期, [假日]) |
| IF          | 條件判斷         | IF(條件, 值_if_true, 值_if_false)       |
| IFS         | 多重條件判斷     | IFS(條件1, 結果1, 條件2, 結果2, ...)    |
| AND         | 同時滿足多個條件 | AND(條件1, 條件2, ...)                  |
| OR          | 任一條件成立     | OR(條件1, 條件2, ...)                   |
| NOT         | 邏輯反轉         | NOT(條件)                               |
| IFERROR     | 錯誤處理         | IFERROR(值, 錯誤時回傳值)               |
| IFBLANK     | 空白判斷與替換   | IF(A1="","待補",A1)                     |

## 進階函數與資料處理 

### 課程目標

* 掌握資料比對（搜尋/參照）、陣列運算與動態範圍設定技巧
* 熟悉進階文字處理與清理方法，能處理雜訊型資料

### 文字處理函數  

#### 文字合併函數

##### CONCAT

* 定義：合併多個範圍或字串中的文字。此函式為 CONCATENATE 的現代取代者，主要優點是支援「範圍」選取。
* 使用語法：CONCAT(文字1, [文字2], ...)
* 實際範例：`=CONCAT(A2:C2)`。
  * 說明：假設 A1 是「台北市」、B1 是「信義區」、C1 是「市府路」，此公式會將 A1 到 C1
    範圍內所有儲存格的內容合併，傳回 "台北市信義區市府路"。注意：CONCAT 不會在項目之間自動加入分隔符號。

##### TEXTJOIN

* 定義：合併多個範圍或字串中的文字，並且可以在每個項目之間插入指定的「分隔符號」，還能選擇是否「忽略空格」。是
  目前功能最強大、最有彈性的文字合併函式。
* 使用語法：`TEXTJOIN(分隔符號, 是否忽略空格, 文字1, [文字2], ...)`
* 實際範例：`=TEXTJOIN(", ", TRUE, A2:C2)`。
  * 說明：此公式會將 A1:A5 範圍內的所有文字合併。每個項目之間會用「, 」(逗號和空格) 作為分隔符號。TRUE：代表如果範圍中有空白儲存格，將會被忽略。例如，若 A1:A3 為 "蘋果", "橘子", "香蕉"，結果會是 "蘋果, 橘子, 香蕉"。

>實作練習：在員工資料表新增一個欄位，將姓名，部門及月薪合併成一筆資料，並且以"，"隔開。

```excel
=TEXTJOIN(", ", TRUE, A2:C2,F2)
```

#### 文字分析函數

##### LEN

* 定義：傳回文字字串的「字元」個數。
* 使用語法：LEN(文字字串)
* 實際範例：`=LEN("Excel 學習")`。
  * 說明：計算 "Excel 學習" 的字元總數。E,x,c,e,l, ,學,習 共 8 個字元，因此公式會傳回 8。
  * 相關函式：LENB 則是計算「位元組(Byte)」數，一個中文字會算成 2，所以 =LENB("Excel 學習") 會傳回 10
    (6個英數字元 + 2*2個中文字元)。

---

##### LEFT

* 定義：從一個文字字串的開頭 (最左邊) 起，傳回指定數量的「字元」。
* 使用語法：LEFT(文字字串, [取幾個字])
* 實際範例：`=LEFT("桃園市桃園區", 3)`。
  * 說明：傳回「台北市信義區」這個字串中最左邊的 3 個字元，結果為 "台北市"。

##### RIGHT

* 定義：從一個文字字串的結尾 (最右邊) 起，傳回指定數量的「字元」。
* 使用語法：RIGHT(文字字串, [取幾個字])
* 實際範例：`=RIGHT("SKU-2025-XYZ", 3)`。
  * 說明：傳回「SKU-2025-XYZ」這個字串中最右邊的 3 個字元，結果為 "XYZ"。

##### MID

* 定義：從一個文字字串的中間，於指定起始位置起，傳回指定數量的「字元」。
* 使用語法：MID(文字字串, 開始, 取幾個字)
* 實際範例：`=MID("SKU-2025-XYZ", 5, 4)`。
  * 說明：從「SKU-2025-XYZ」這個字串的第 5 個字元 (2) 開始，擷取 4 個字元，結果為 "2025"。

>實作練習：開啟 job104.xlsx 在欄位地區後面新增兩欄分別為市跟區，將地區分割後分別填入。

```excel
=MID(C2, 1, 3)
=IF(MID(C2, 4, 3)="","無",MID(C2, 4, 3))
```

##### FIND

* 定義：在一個文字字串中尋找另一個文字字串，並傳回其起始位置的編號。此函式會**區分英文大小寫**，且不支援萬用字元。
* 使用語法：FIND(查詢字串, within_text, [start_num])
* 實際範例：`=FIND("-", "SKU-2025-XYZ")`。
  * 說明：在「SKU-2025-XYZ」字串中尋找第一個「-」出現的位置，結果為 4。若使用 =FIND("xyz", "SKU-2025-XYZ")
    則會因大小寫不符而傳回 #VALUE! 錯誤。

##### SEARCH

* 定義：在一個文字字串中尋找另一個文字字串，並傳回其起始位置的編號。此函式**不區分英文大小寫**，且支援萬用字元 ( ? 和 * )。
* 使用語法：`SEARCH(find_text, within_text, [start_num])`
* 實際範例：`=SEARCH("xyz", "SKU-2025-XYZ")`。
  * 說明：在「SKU-2025-XYZ」字串中尋找 "xyz"。因為 SEARCH 不區分大小寫，所以它會成功找到大寫的
    "XYZ"，並傳回其起始位置 10。

>實作練習：開啟 job104.xlsx 在欄位公司名稱後面新增一欄，取出公司名稱後填入(只保留公司名稱)。

```
=LEFT(A3,SEARCH("有限",A3)-1)
```

#### 文字轉換函數

##### UPPER

   * 定義：將文字字串中的所有小寫英文字母轉換成「大寫」。
   * 使用語法：UPPER(文字字串)
   * 實際範例：`=UPPER("Hello World")`。
     * 說明：將「Hello World」這個字串轉換成全大寫，結果為 "HELLO WORLD"。

##### LOWER

* 定義：將文字字串中的所有大寫英文字母轉換成「小寫」。
* 使用語法：LOWER(文字字串)
* 實際範例：`=LOWER("Hello World")`。
  * 說明：將「Hello World」這個字串轉換成全小寫，結果為 "hello world"。

##### PROPER

* 定義：將文字字串中每個英文單字的**「字首」**轉換為大寫，其餘字母則轉換為小寫。
* 使用語法：`PROPER(文字字串)`
* 實際範例：`=PROPER("welcome to taipei")`。
  * 說明：將「welcome to taipei」這個字串轉換成字首大寫格式，結果為 "Welcome To Taipei"。

##### TRIM

* 定義：移除文字字串中多餘的「空格」，只保留單字之間的一個空格。常用於清理從外部匯入、帶有不規則空格的資料。
* 使用語法：`TRIM(文字字串)`
* 實際範例：`=TRIM("  Hello   World  ")`。
  * 說明：移除字串前後所有空格，並將字串中間的多個空格壓縮成一個，結果為 "Hello World"。

---

##### REPLACE

* 定義：根據指定的「起始位置」與「長度」，將文字字串的一部分替換成另一個不同的文字字串。

* 使用語法：`REPLACE(文字字串, 從第幾個字, 修改字數, 替換字串)`

* 實際範例：`=REPLACE("ABC-123-XYZ", 5, 3, "456")`。

  * 說明：在「ABC-123-XYZ」字串中，從第 5 個字元 (1) 開始，將 3 個字元 (123) 替換成 "456"，結果為
    "ABC-456-XYZ"。

  >實作練習：將農產品交易行情的種類代碼 N05 改為 NO-05

  ```excle
  =REPLACE(B2, 1, 1, "NO-")
  ```

##### SUBSTITUTE

* 定義：在文字字串中，將指定的「舊文字」替換成「新文字」。您可以選擇要替換第幾個出現的舊文字。
* 使用語法：`SUBSTITUTE(文字字串, 替換字串, 新字串, [第幾個])`
* 實際範例：=SUBSTITUTE("2025/10/15", "/", "-")。
  * 說明：在「2025/10/15」字串中，將所有出現的 / 都替換成 -，結果為 "2025-10-15"。
* 進階範例：`=SUBSTITUTE("A-B-C-D", "-", "*", 2)`。
  * 說明：只將第 2 個出現的 - 替換成 *，結果為 "A-B*C-D"。

##### TEXTSPLIT

* 定義：使用指定的分隔符號，將文字字串「分割」成多個部分，並將結果動態展開到多個儲存格中 (可以是橫向一列或縱向一欄)。這是取代傳統「資料剖析」功能的現代函式。

* 使用語法：`TEXTSPLIT(文字, 欄分隔符號, [列分隔符號], [是否忽略空格], ...)`

* 實際範例 1 (分割至不同欄)：`=TEXTSPLIT("蘋果,橘子,香蕉", ",")`

  * 說明：此公式會使用「,」(逗號) 作為欄分隔符號，將文字 "蘋果,橘子,香蕉"
    分割。結果會自動向右「溢出」到三個相鄰的儲存格中，分別顯示「蘋果」、「橘子」、「香蕉」。

* 實際範例 2 (分割至不同列)：`=TEXTSPLIT("蘋果;橘子;香蕉", , ";")`

  * 說明：此公式省略了「欄分隔符號」(第二個參數留空)，並使用「;」(分號)
    作為列分隔符號。結果會自動向下展開到三個儲存格中。

  

> 如何使用函式處理薪資範圍後分別放入欄位B及欄位C?

使用 TEXTSPLIT 函數，非常簡潔高效。您只需要在 B1 儲存格輸入一個公式，它會自動將兩個結果「溢出 (Spill)」到 B1 和 C1。

  步驟：

      1. 先用 TEXTAFTER 和 TEXTBEFORE 去掉頭尾的文字。
      2. 再用 TEXTSPLIT 以 ~ 符號為分隔符，將文字拆開。
      3. 最後用 SUBSTITUTE 去掉千分位逗號 ,，並用兩個負號 -- 將文字轉換為數字。

  在 B1 儲存格輸入以下公式：
   1 =--SUBSTITUTE(TEXTSPLIT(TEXTBEFORE(TEXTAFTER(A1,"月薪"),"元"),"~"),",","")

  公式解析：

   * TEXTAFTER(A1,"月薪"): 取得 "月薪" 後面的文字，結果為 "32,000~39,000元"。
   * TEXTBEFORE(...) : 從上一步的結果中，取得 "元" 前面的文字，結果為 "32,000~39,000"。
   * TEXTSPLIT(...,"~"): 將上一步的結果用 "~" 拆開，得到 {"32,000", "39,000"} 這樣一個陣列。Excel 會自動將這兩個值分別放入 B1 和 C1。
   * SUBSTITUTE(...,",",""): 去掉陣列中每個值的逗號，得到 {"32000", "39000"}。
   * --: 這是將文字轉換為數字的速記法，等同於 VALUE() 函數。最終得到 {32000, 39000}。

### 動態陣列函數

##### UNIQUE

* 定義：從一個指定的範圍或陣列中，傳回一組不重複的「唯一值」清單。這是一個「動態陣列 (Dynamic Array)」函式，其結果會自動「溢出 (Spill)」到下方的儲存格中，無需手動向下拖曳公式。
* 使用語法：`UNIQUE(範圍, [依欄位比較], [只出現一次])`
  * 範圍 (array)：您要從中擷取唯一值的來源範圍。
  * [依欄位比較] (by_col)：(選填) **預設為 FALSE (逐列比較)**。若設為 TRUE 則會變成逐欄比較。
  * [只出現一次] (exactly_once)：(選填)
    * FALSE (預設)：傳回**所有不重複**的項目 (例如 A,B,A,C 會傳回 A,B,C)。
    * TRUE：只傳回在來源中**僅出現過一次**的項目 (例如 A,B,A,C 只會傳回 B,C)。
* 實際範例: 

範例 1：取得不重複的清單 (預設用法)

情境：從 A 欄的產品清單中，建立一份不重複的產品列表。

公式：`=UNIQUE(A2:A9)`

* 說明：這是最常見的用法。函式會掃描 A2:A9，找出所有不重複的產品名稱，並在您輸入公式的儲存格及其下方自動建立一個新的清單。
* 結果 (會自動溢出到四個儲存格)：


範例 2：只傳回「出現過一次」的項目

情境：從 A 欄的產品清單中，找出哪些產品是只被記錄過一次的。

公式：`=UNIQUE(A2:A9, , TRUE)`

* 說明：注意我們省略了第二個引數，並將第三個引數設為 TRUE。函式會找出在來源清單中「從頭到尾只出現過一次」的項目。「蘋果」和「橘子」都出現了兩次，所以被排除了。

>練習: 在員工資料表列出公司所有部門及每個部門有幾名員工

```excel
=UNIQUE(B2:B191)
=COUNTIF(B:B,J2)
```

##### SEQUENCE

* 定義：產生一組連續的「數列」，例如 1, 2, 3...。可以自由指定要產生的「列數」、「欄數」、起始值和間距。
* 使用語法：`SEQUENCE(列數, [欄數], [起始值], [間距])`
* 實際範例 1：`=SEQUENCE(10)`
  *  [learn_excel_01.md](learn_excel_01.md) 說明：產生一個 10 列 1 欄的數列，從 1 到 10。
* 實際範例 2：`=SEQUENCE(3, 4, 10, 5)`
  * 說明：產生一個 3 列 4 欄的矩陣。數列從 10 開始，每個數字之間的間距為 5 (即 10, 15, 20, ...)。

##### FILTER

* 定義：根據指定的「條件」，從一個資料範圍中「篩選」出所有符合條件的紀錄。這是一個動態陣列函式，結果會自動溢出。

* 使用語法：`FILTER(要篩選的範圍, 條件, [如果沒有結果時])`
* 實際範例：`=FILTER(A2:C100, B2:B100="業務部", "查無資料")`
  * 說明：從 A2:C10 範圍中，篩選出所有 A 欄等於「業務部」的紀錄。如果找不到任何紀錄，則顯示「查無資料」。


>練習: 在農產品交易行情取出所有椪柑交易資料

```excel
=FILTER(農產品交易行情!D2:K8276, 農產品交易行情!D2:D8276="椪柑", "查無資料")
```



##### HSTACK

* 定義：將多個範圍或陣列「水平地 (Horizontally)」堆疊在一起，組合成一個更寬的新陣列。
* 使用語法：`HSTACK(範圍1, [範圍2], ...)`
* 實際範例：`=HSTACK(A1:A5, D1:D5)`
  * 說明：將 A1:A5 範圍和 D1:D5 範圍的資料，左右並排成一個新的表格。結果會是一個 5 列 2 欄的陣列。
  * 相關函式：其對應的函式是 VSTACK，用於「垂直地」堆疊資料。

>練習: 在農產品交易行情產生一個新表格，欄位為作物名稱，平均價，交易量及總成交值

```excel
=HSTACK(D2:D10, J2:J10, K2:K10)
=N1*O1
```



##### SORT

* 定義：對一個範圍或陣列的內容進行「排序」。它會傳回一個已排序的新陣列，而不會改動到您的原始資料。
* 使用語法：`SORT(範圍, [排序列索引], [排序順序], [按欄排序])`
* 實際範例 1：`=SORT(A2:A10)`
  * 說明：對 A2:A10 範圍的內容進行**預設的「遞增」排序** (A-Z 或小到大)。
* 實際範例 2：=SORT(A2:C10, 3, -1)
  * 說明：對 A2:C10 整個範圍進行排序。排序的依據是第 3 欄 (C 欄)。排序的順序為 -1，代表「遞減」排序
    (大到小)。



### 搜尋與參照函數

**課程內容：**

* **LOOKUP 函數家族介紹與比較**

  * VLOOKUP：垂直搜尋（左到右），搜尋欄位必須在範圍最左側
  * HLOOKUP：水平搜尋（上到下），用法與 VLOOKUP 
  * XLOOKUP（Excel 365 可選）：功能完整，支援反向搜尋、未找到回傳值、精確/近似參照

##### VLOOKUP (垂直查詢)

* 定義：在表格的「**最左欄**」中進行「垂直查詢」，並傳回同一列中指定欄位的數值。
* 使用語法：`VLOOKUP(查詢值, 表格範圍, 欄位索引, [是否完全符合])`
  * 查詢值：要比對的關鍵欄位，如 ID 或編號
  * 表格範圍：包含要查詢的值及要擷取資料的完整表格範圍
  * 欄位索引：從左數過來**第幾欄資料要被擷取**( 數字 1,2,3... )
  * 是否完全符合：通常使用  FALSE(精確比對)以避免錯誤。

* 實際範例：`=VLOOKUP(C5,C2:F19,4,FALSE)`。
  * 說明：在員工資料表 C2:F19 的範圍中，尋找最左欄 (C欄) 中值為 C5 的儲存格，找到後，傳回該列的第 4 欄 (F欄) 的值。**FALSE 代表要求「完全符合」**。
    ![](C:\Users\troie\Documents\WORKS\maolahdesign.github.io\project\lccnet\智能報表應用\pto01.png)

##### HLOOKUP (水平查詢)

* 定義：在表格的「最頂列」中進行「水平查詢」，並傳回同一欄中指定列位的數值。

* 使用語法：`HLOOKUP(查詢值, 表格範圍, 列位索引, [是否完全符合])`
  * 查詢值: 您要查詢的值（例如 "三月"）。
  * 表格範圍: 包含資料的整個範圍（例如 A1:D5）。
  * 列位索引: 您想從中取值的列號。在表格範圍中，第一列是 1，第二列是 2，依此類推。
  * [是否完全符合]:
    * FALSE 或 0：要求完全符合的查詢值。 (最常用)
    * TRUE 或 1：允許近似符合的查詢值。使用此選項時，第一列的值必須是遞增排序。

* 實際範例：`=HLOOKUP("平均價",D1:K11,4,FALSE)`
  * 說明：在 D1:K11 的範圍中，尋找最頂列 (第1列) 中值為 "平均價" 的儲存格，找到後，傳回該欄的第 4 列 (第4列) 的值。FALSE 代表要求「完全符合」。
    ![](C:\Users\troie\Documents\WORKS\maolahdesign.github.io\project\lccnet\智能報表應用\pto02.png)

>實作練習：新增員工資料表2，欄位為姓名、部門、電話及月薪，隨機複製五個姓名，利用 VLOOKUP 取得對應資料值

```excel
=VLOOKUP(A2,員工資料表!A2:F40,2,FALSE)
=VLOOKUP(A2,員工資料表!A2:F40,3,FALSE)
=VLOOKUP(A2,員工資料表!A2:F40,6,FALSE)
```

更簡潔的方式

```
=VLOOKUP(A2, 員工資料表!A2:F40, {2, 3, 6}, FALSE)
```

解說：

      1. `{2, 3, 6}`我們用大括號 {} 將想回傳的多個欄位索引包起來，形成一個「水平陣列常數」。
      2. 運作方式：當 VLOOKUP 找到 A2 對應的資料列後，它會看到您要求回傳第 2、第 3、和第 6 欄的資料。
      3. 自動溢出 (Spill)：在動態陣列的支援下，Excel 會將這三個欄位的結果，自動「溢出」到您輸入公式的儲存格以及右側相鄰的兩個儲存格中。

##### XLOOKUP

* 定義：新世代的萬用查詢函式，可以在一個範圍或陣列中尋找項目，並傳回對應的結果。它能取代 VLOOKUP 和 HLOOKUP，且功能更強大、使用更靈活。
* 使用語法：`XLOOKUP (查詢值, 查詢範圍, 回傳範圍, [找不到時], [比對模式], [搜尋模式])`

  - `[找不到時] ([if_not_found])`

    * 選用參數
    * 定義：如果 XLOOKUP 在 查詢範圍 中找不到 查詢值，它會回傳這個參數所指定的值。
    * 範例："查無此人" 或 0。
    * 重點提示：
      * 如果省略此參數，當找不到時，XLOOKUP 會回傳 #N/A 錯誤。
      * 這個參數取代了過去需要用 IFERROR 函數來包裝 VLOOKUP 的做法，讓公式更簡潔。

  - `[比對模式] ([match_mode])`

    * 選用參數
    * 定義：指定 XLOOKUP 如何進行比對。
    * 可選值：
      * 0 (預設值)：精確比對 (Exact match)。這是最常用的模式，要求 查詢值 必須與 查詢範圍 中的項目完全相同。
      * -1：精確比對或下一個較小項目 (Exact match or next smaller item)。如果找不到精確比對，則回傳下一個比 查詢值
        小的項目。常用於查找級距（例如根據分數查找等級，或根據金額查找折扣）。
      * 1：精確比對或下一個較大項目 (Exact match or next larger item)。如果找不到精確比對，則回傳下一個比 查詢值 大的項目。
      * 2：萬用字元比對 (Wildcard match)。允許您在 查詢值 中使用 * (代表任意多個字元) 和 ? (代表單一字元) 進行模糊比對。
    * 重點提示：
      * 預設為 0 (精確比對)，這比 VLOOKUP 的預設值（近似比對）更安全，減少了錯誤發生的機率。
      * 使用 -1 或 1 時，查詢範圍 建議先進行排序，以確保結果的準確性。

  - `[搜尋模式] ([search_mode])`

    * 選用參數
    * 定義：指定 XLOOKUP 在 查詢範圍 中搜尋的方向。
    * 可選值：
      * 1 (預設值)：從第一個項目開始搜尋 (Search from first to last)。這是最常見的搜尋方向。
      * -1：從最後一個項目開始搜尋 (Search from last to first)。非常有用！例如，當您想找到某個員工的「最新」一筆紀錄時。
      * 2：二分搜尋 (遞增排序) (Binary search, ascending sort)。這是一種非常快速的搜尋演算法，但要求 查詢範圍必須是遞增排序的。如果資料量非常大，可以顯著提升效能。
      * -2：二分搜尋 (遞減排序) (Binary search, descending sort)。同樣要求 查詢範圍 必須是遞減排序的。
    * 重點提示：
      * 預設為 1。
      * 當您需要查找「最新」或「最後」一筆符合條件的紀錄時，將此參數設為 -1 會非常方便。
      * 使用 2 或 -2 時，請務必確認 查詢範圍 已經排序，否則會回傳錯誤的結果。

* 實際範例：`=XLOOKUP(A2,員工資料表!A2:A40,員工資料表!F2:F40)`。
  * 說明：在 員工資料表!A2:A40 (查詢範圍) 中尋找 "A2"，並從 員工資料表!F2:F40 (回傳範圍) 中傳回同一個位置的值。XLOOKUP
    **預設為「完全符合」，且查詢與回傳範圍是分開的，使用上更安全直觀**，也沒有 VLOOKUP 只能查最左欄的限制。

>實作練習：在員工資料表2，增加一個欄位調薪，每個員工增加 4%

```excel
=XLOOKUP(G2,員工資料表!A$2:A$40,員工資料表!E$2:E$40)*1.04
```

##### MATCH (比對)

* 定義：在一個單行或單列的範圍中搜尋指定的項目，並傳回該項目在範圍中的「相對位置」(是第幾個)。
* 使用語法：`MATCH(查詢值, 查詢範圍, [比對類型])`
* 實際範例：`=MATCH("蔡宏蓉",A2:A12,0)`。
  * 說明：在 A1:A20 的範圍中，尋找 "張經理" 這個文字。如果 "張經理" 位於 A15 儲存格，此公式就會傳回 15。0 代表要求「完全符合」。

##### INDEX (索引)

* 定義：傳回表格或範圍中，指定「**列號**」與「**欄號**」交叉位置的儲存格內容。它的作用就像是從地圖中根據座標找出對應的位置。
* 使用語法：INDEX(範圍, 列號, [欄號])
* 實際範例：`=INDEX(E2:E12, 8)`。
  * 說明：在 E1:E12 的範圍中，找出第 8 列，也就是 E8，並傳回其內容。

##### INDEX + MATCH (組合應用)

這兩個函式組合起來，是 Excel 中取代 VLOOKUP 的經典、強大且靈活的查詢方法。

* 定義：MATCH 負責找到目標在哪一「列」，INDEX 則根據 MATCH 傳回的列號，從您指定的結果範圍中取出對應的值。
* 使用語法：`=INDEX(回傳值的範圍, MATCH(查詢值, 查詢值的範圍, 0))`
* 實際範例：`=INDEX(E2:E12, MATCH("蔡宏蓉",A2:A12,0))`。
  * 說明：
    1. MATCH("蔡宏蓉",A2:A12,0) 會先在 A 欄中找到 "蔡宏蓉" 的位置 (8)。
    2. 公式變成 =INDEX(E2:E12, 8)。
    3. INDEX 接著會去 E 欄中，找出第 8 個位置的值並傳回。
  * 這個組合**沒有 VLOOKUP 只能從最左欄查詢的限制**，更為靈活。

> 實作練習：使用 INDEX + MATCH 在農產品交易行情表取得香蕉的交易量

```
=INDEX(K2:K30,MATCH(D20,D2:D30,0))
```



---

### 實作練習 1：用 VLOOKUP 與 INDEX+MATCH 對同一筆查詢做比較

**目的**：理解 VLOOKUP 受限於查詢欄位置，而 INDEX+MATCH 更靈活。

**步驟**：

1. **準備測試資料**

   | 員工編號 | 部門 | 總銷售 |
   | -------- | ---- | ------ |
   | 1001     | 行銷 | 5000   |
   | 1002     | 財務 | 4000   |
   | 1003     | 行銷 | 6000   |

2. **使用 VLOOKUP 查詢**

   * 公式：`=VLOOKUP(1002, A2:C4, 3, FALSE)`
   * 說明：查找員工編號 1002，回傳第 3 欄（總銷售）
   * 結果：4000

3. **使用 INDEX+MATCH 查詢**

   * 公式：`=INDEX(C2:C4, MATCH(1002, A2:A4, 0))`
   * 說明：MATCH 找到 1002 在 A2:A4 的位置，再用 INDEX 回傳 C 欄對應值
   * 結果：4000

4. **觀察欄位順序變動**

   * 將「總銷售」欄移到最左邊
   * 再試一次 VLOOKUP：`=VLOOKUP(1002, A2:C4, 3, FALSE)`

     * 結果可能錯誤，因為 VLOOKUP 只能向右查詢
   * INDEX+MATCH 仍正確：`=INDEX(C2:C4, MATCH(1002, B2:B4, 0))`

**結論**：VLOOKUP 受限於查詢欄在左側；INDEX+MATCH 適合欄位順序會變動的情況。

---

### 實作練習 2：使用 INDEX+MATCH 完成多條件查找

**目的**：實作「同時比對員工編號與部門」的查找，並用 IFERROR 避免 #N/A

**步驟**：

1. **準備測試資料**

   | 員工編號 | 部門 | 總銷售 |
   | -------- | ---- | ------ |
   | 1001     | 行銷 | 5000   |
   | 1002     | 財務 | 4000   |
   | 1003     | 行銷 | 6000   |
   | 1002     | 行銷 | 3000   |

2. **建立多條件查找公式**

   * 公式（需 Ctrl+Shift+Enter）：

     ```
     =IFERROR(
       INDEX(C2:C5, MATCH(1, (A2:A5=1002)*(B2:B5="行銷"), 0)),
       "無資料"
     )
     ```

   * 說明：

     1. `(A2:A5=1002)` 會生成一個 TRUE/FALSE 陣列
     2. `(B2:B5="行銷")` 也生成 TRUE/FALSE 陣列
     3. 兩個陣列相乘 → 只有同時符合兩條件的位置為 1
     4. MATCH 找到 1 的位置，INDEX 回傳對應總銷售
     5. IFERROR 若找不到，回傳「無資料」

3. **結果驗證**

   * 查找員工編號 1002 且部門「行銷」 → 回傳 3000
   * 查找員工編號 1001 且部門「財務」 → 回傳「無資料」

4. **可擴展操作**

   * 改查其他條件組合
   * 將公式拖拉應用到其他查詢列

## 數據分析與視覺化

### 樞紐分析表基礎與進階功能

* **內容解說：**

  *   **樞紐分析是什麼：** 解釋樞紐分析表是 Excel 中最強大的互動式數據匯總工具，能快速將雜亂的明細數據，轉換成有意義的交叉分析報表。
  *   **四大欄位區塊：** 詳細介紹「**篩選**」、「**欄**」、「**列**」、「**值**」四個區域的功能與應用場景，這是樞紐分析的基石。
  *   **基礎操作：** 從建立第一張樞紐分析表開始，學習如何拖拉欄位、變更值的計算方式（總計、計數、平均）。
  *   **進階功能：**
      1.  **交叉分析篩選器 (Slicer) & 時間表 (Timeline)：** 讓報表從靜態變為動態，實現儀表板般的互動式篩選體驗。
      2.  **群組功能：** 將日期（年/季/月）、數字（價格區間）進行分組，以更高維度進行分析。
      3.  **計算欄位 (Calculated Field)：** 在樞紐分析表內直接建立新欄位（如：毛利率、客單價），無需修改原始數據。
      4.  **值的顯示方式：** 將數值變更為「佔總和百分比」、「差異百分比」等，進行深度比較分析。

* **操作範例：**

  * **範例資料：** 農產品交易行情表。

  * **操作任務：**

    1. 選擇**插入**標籤後點擊**樞紐分析表**

    2. 在工作表標籤選擇滑鼠右鍵選擇**重新命名**

    3. 透過**勾選或拖曳方式**將欄位放入四大欄位區塊
       1. 產品名稱，平均價，交易量

       2. 將市場加入到列

       3. 只選擇幾個主要市場

       4. 將市場移至欄

       5. **修改無交易空格欄位**
          1. 在空格上按滑鼠右鍵，選擇**樞紐分析表選項**
          2. 在 若為空白儲存格，顯示 填入0，按**確定**完成修改

       6. **檢視詳細交易資料** : 在儲存個數值上雙擊滑鼠左鍵

       7. **與原表格保持連動** : 原表格數據做任何新增或修改後，在樞紐分析表按滑鼠右鍵，選擇重新整理即可進行資料同步。
          另一個方法可選擇資料標籤，點擊**全部重新整理** 。

    4. **任務A (基礎)：** 建立樞紐分析表，分析「市場」在「產品類別」的「總銷售額」。

    5. **任務B (進階)：**
       * 新增「銷售額」的**計算欄位** (`= 平均價 * 交易量`)。
         1. 點擊功能區中的「**樞紐分析表分析** 」索引標籤。
         2. 在「計算 (Calculations)」群組中，點擊「**欄位、項目與集合**」按鈕。
         3. 在下拉選單中，選擇「**計算欄位**」。
         4. 此時會彈出一個「**插入計算欄位**」的對話框。
         5. 名稱 (Name)：
            * 在「名稱」欄位中，輸入您新欄位的名稱，例如：銷售額。
         6. 公式 (Formula)：
            * 在「公式」欄位中，您會看到預設的 =0。請先將 0 刪除。
            * 在下方的「欄位 (Fields)」清單中，找到 平均價。
            * 雙擊 平均價 (或點選 平均價 後再點擊「插入欄位」按鈕)，它會被插入到公式欄位中。
            * 在公式欄位中輸入 * (乘號)。
            * 在「欄位」清單中，找到 交易量。
            * 雙擊 交易量 (或點選 交易量 後再點擊「插入欄位」按鈕)，它會被插入到公式欄位中。
            * 現在，您的公式欄位應該顯示為：=平均價 * 交易量。
         7. 新增並確定：
            * 點擊「新增 (Add)」按鈕。
            * 點擊「確定 (OK)」按鈕。
       * **新增「市場」和「作物名稱」的交叉分析篩選器**。
         1. ctrl + A 全選資料表單，然後 ctrl + C 複製
         2. 在右邊空白儲存格貼上
         3. 選擇**插入**標籤，然後點選**交叉分析篩選器**
         4. 在篩選器上按滑鼠右鍵選擇**顯示設定**
         5. 捲動右邊設定找到樞紐分析表連線，確認勾選樞紐分析表 1 及分析表 2
       * **將「銷售額」複製一份，並將其值的顯示方式改為「佔總和百分比」**。
         1. 在「**樞紐分析表欄位**」窗格的「**值** 」區域中。
         2. 點擊新複製出來的「**總銷售額**」欄位旁邊的下拉箭頭。
         3. 選擇「**值欄位設定**」。
         4. 在跳出的「**值欄位設定**」對話框中，切換到「**顯示值方式** 」索引標籤。
         5. 在「**顯示值方式**」的下拉選單中，選擇「**總計百分比**」。
         6. 點擊「確定」。

    > 實作練習：使用銷售紀錄表完成以下任務
    >
    > 1. 建立樞紐分析表
    > 2. 將「銷售額」複製一份，並將其值的顯示方式改為「佔總和百分比」
    > 3. 新增「市場」和「產品類別」的交叉分析篩選器

---

### 統計圖表的設計與應用

* **內容解說：**

  *   **圖表是數據的語言：** 強調圖表的目的在於「講故事」，快速傳達數據背後的洞見。
  *   **從樞紐分析到樞紐分析圖：** 學習如何一鍵將樞紐分析表轉換為連動的樞紐分析圖，讓數據分析結果視覺化。
  *   **選擇正確的圖表類型：**
      *   **長條圖/柱狀圖：** 用於比較不同類別的數值大小。
      *   **折線圖：** 用於呈現隨時間變化的趨勢。
      *   **圓餅圖：** 用於顯示各部分佔整體的比例（提醒：類別過多時不適用）。
      *   **組合圖：** 將不同度量單位（如銷售額與成長率）呈現在同一張圖上，進行多維度比較。
  *   **圖表設計原則：** 介紹如何美化圖表，包括移除不必要的格線、加上清晰的標題與資料標籤，讓圖表簡潔有力。

* **操作範例：**

  *   **範例資料：** 延續單元一的樞紐分析表結果。
  *   **操作任務：**
      1.  **任務A：** 基於分析「各區域每月銷售額」的樞紐分析表，建立一張**折線圖**，呈現各區域的銷售趨勢。
      2.  **任務B：** 建立一張**組合圖**，同時呈現各產品類別的「總銷售額」（柱狀圖）與「毛利率」（折線圖，使用副座標軸）。

  > 實作練習：使用銷售紀錄表完成以下任務
  >
  > 完成「2007 年每月銷售額」的樞紐分析表，並建立一張**折線圖**，呈現全年的銷售趨勢。

---

### 資料透視與結構化分析

*   **內容解說：**
    *   **Garbage In, Garbage Out：** 強調數據品質的重要性。結構混亂的資料無法進行有效分析。
    *   **何謂「結構化資料 (Tidy Data)」：** 介紹標準的清單式資料結構（一列一筆記錄，一欄一個變數），這是樞紐分析與多數分析工具的基礎要求。
    *   **常見的非結構化資料：** 展示常見的錯誤格式，如合併儲存格、多層標題、以及將月份當作欄位的「交叉表」。
    *   **Power Query 逆樞紐 (Unpivot)：** Excel 內建的強大 ETL (萃取、轉換、載入) 工具 Power Query，並聚焦於其核心功能「逆樞紐分析行」，能快速將交叉表轉換為結構化清單。

### 逆樞紐操作範例

* **範例資料：** 一份產品銷售報告，第一欄是「產品名稱」，後續欄位分別是「一月銷售額」、「二月銷售額」、「三月銷售額」...

* **操作任務：**

  - 使用 Power Query 載入資料

    1. 選取表格內任一儲存格。
    2. 前往 Excel 頂端功能區的「資料 (Data)」索引標籤。
    3. 在「**取得及轉換資料**」群組中，點擊「**從表格/範圍** 」。
    4. 此時，Power Query 編輯器會開啟，並載入您的銷售資料。

  - 執行「**取消其他資料行的樞紐**」
    這是核心步驟，將寬表格轉換成長表格。

    1. 在 Power Query 編輯器中，選取「產品名稱」這一欄。
       * 為什麼選「產品名稱」？ 因為這是您不希望被逆樞紐的識別欄位。您希望它保持不變，而其他月份欄位則被「融化」到它下方。
    2. 在選取的「產品名稱」欄位標題上按一下滑鼠右鍵。
    3. 在跳出的選單中，選擇「**取消其他資料行的樞紐**」。
       * 解說：這個功能會將所有未被選取的欄位（即「一月銷售額」、「二月銷售額」、「三月銷售額」）進行逆樞紐操作。

  - 重新命名新產生的欄位

    執行逆樞紐後，您會看到原本的月份欄位消失了，取而代之的是兩個新欄位，預設名稱為「屬性 (Attribute)」和「值 (Value)」。

    1. 重新命名「屬性」欄位：
       * 在「屬性」欄位標題上按兩下（或按右鍵選擇「重新命名」）。
       * 將其名稱修改為「月份」。
    2. 重新命名「值」欄位：
       * 在「值」欄位標題上按兩下。
       * 將其名稱修改為「銷售額」。

    現在，您的資料已經是乾淨的「產品名稱」、「月份」、「銷售額」三欄結構化資料了！

  - 載入整理好的資料

    1. 在 Power Query 編輯器的左上角，點擊「常用 (Home)」索引標籤。
    2. 點擊「關閉並載入 (Close & Load)」按鈕。
    3. 在下拉選單中，選擇「關閉並載入至... (Close & Load To...)」。
    4. 在「載入至」對話框中，選擇「表格 (Table)」和「新工作表 (New Worksheet)」。
    5. 點擊「確定 (OK)」。

---

### 自動化數據處理範例

*   **內容解說：**
    *   **告別重複的手動操作：** Power Query 的所有操作步驟都會被記錄下來，形成一個可重複執行的查詢。當來源資料更新或增加時，只需一鍵「重新整理」，所有整理步驟都會自動重跑。
    *   **合併多份檔案：** 介紹 Power Query 最令人驚豔的功能之一：從資料夾合併多個結構相同的檔案（Excel 或 CSV）。這對於需要匯總每月、每週或各分店報表的工作極為高效。
    *   **數據格式轉換：** 在 Power Query 編輯器中，可以輕鬆完成資料類型轉換（如將文字格式的日期轉為日期格式）、分割欄位、取代文字等常見的數據清理工作。
*   **操作範例一：**
    *   **範例資料：** 一個資料夾，內含三份結構完全相同的銷售報表：台北一.csv, 台北二.csv, 板橋區.csv。
    *   **操作任務：**
        1.  開啟 Power Query，點選**資料**選單>取得資料，選擇「**從檔案**」>「**從資料夾**」。
        2.  選取包含三份報表的資料夾，點擊「**合併與轉換資料**」。
        3.  Power Query 會自動將所有檔案的內容合併成一個大的資料表。
        4.  **驗證：** 將一份新的 `台北市場.csv` 檔案放入同一個資料夾，然後在 Excel 中對查詢按下滑鼠右鍵選擇「**重新整理**」，觀察到4月的數據被自動合併進來。
*   **操作範例二：**
    *   **範例資料：** 將農產品交易行情資料表內各市場資料進行匯總。
    *   **操作任務：**
        1.  方法一 : 複製貼上
        2.  方法二 : VBA
        3.  選擇台北一工作表的任一儲存格。
        4.  開啟 Power Query，點選資料選單>取得資料，選擇「**從檔案**」>「**從excel 活頁簿**」。
        5.  選擇**農產品交易行情.xlsx**
        6.  選取要合併的工作表，點擊「**轉換資料**」。
        7.  選取標籤**常用**的**附加查詢**選單，下拉選取**將查詢附加為新查詢**。
        8.  選取要合併的工作表
        9.  變更工作表名稱**附加一**為**全區**
        10.  選取「**關閉並載入**」
        11.  刪除重複匯入工作表
        12.  Power Query 會自動將所有檔案的內容合併成一個大的資料表。
        13.  **驗證：** 新增一筆資料後按儲存，然後在 合併工作表 中按下滑鼠右鍵選擇「**重新整理**」，觀察新數據是否被自動加進來。        

---

### 逆樞紐分析行

1. 什麼是「逆樞紐分析行」？

   簡單來說，它是一個「將寬表格變成長表格」的魔法按鈕。

   * 寬表格 (Wide Table)：人類喜歡看的格式。通常特徵或數值被當作欄位標題，例如每個月的銷售額分別佔據一欄。
   * 長表格 (Long Table)：電腦和分析工具喜歡的格式。也稱為「Tidy Data」，所有數值都在同一個欄位中，而它們的特徵則在另一個欄位。

   「逆樞紐分析行」的作用，就是把那些作為「欄位標題」的特徵（例如 "一月", "二月"）融化 (Melt) 或 堆疊 (Stack) 下來，變成一欄新的資料。

2. 一個簡單的視覺化範例

   文字可能有點抽象，讓我們看一個「之前 vs. 之後」的例子，您馬上就能明白。

   處理前：寬表格 (不適合分析的格式)

   假設您的原始報表長這樣，第一欄是產品，後面幾欄分別是不同月份的銷售額。

   

   | 產品 | 一月 | 二月 | 三月 |
   | ---- | ---- | ---- | ---- |
   | 蘋果 | 100  | 120  | 150  |
   | 香蕉 | 80   | 90   | 110  |

   

   問題點：一月、二月、三月
   這些本質上是「時間」的值，卻被當成了欄位。這種格式讓您很難用樞紐分析表去分析「哪種水果的總銷量最高」或「全年的銷售趨勢」。

   處理後：長表格 (適合分析的格式)

   經過「逆樞紐分析行」處理後，表格會變成這樣：

   

   | 產品 | 月份 | 銷售額 |
   | ---- | ---- | ------ |
   | 蘋果 | 一月 | 100    |
   | 蘋果 | 二月 | 120    |
   | 蘋果 | 三月 | 150    |
   | 香蕉 | 一月 | 80     |
   | 香蕉 | 二月 | 90     |
   | 香蕉 | 三月 | 110    |


   發生了什麼事？

   1. 原本的 一月、二月、三月 這三個欄位被「融化」並合併成一個新的欄位，我們將其命名為「月份」。
   2. 原本在這三個欄位下的所有數值，則被集中到另一個新的欄位，我們將其命名為「銷售額」。
   3. 產品 欄位作為固定的識別欄，其內容會被自動向下填充，以匹配新的資料結構。

3. 如何操作？(操作步驟)

   在 Power Query 編輯器中，操作非常簡單：

   1. 載入資料：首先，將您的寬表格載入到 Power Query 編輯器中。

   2. 選取欄位：這是最關鍵的一步。您有兩種策略：
      * 策略A (推薦)：選取不需要被逆樞紐的欄位。在我們的例子中，就是選取「產品」這一欄。
      * 策略B：選取所有需要被逆樞紐的欄位。在我們的例子中，就是同時選取「一月」、「二月」和「三月」這三欄。

   3. 執行逆樞紐：
      * 前往頂端選單的「轉換 (Transform)」索引標籤。
      * 點擊「逆樞紐分析行 (Unpivot Columns)」按鈕的下拉箭頭。
      * 如果您採用策略A，請選擇「取消其他資料行的樞紐 (Unpivot Other
        Columns)」。(這是最常用的選項，因為未來如果增加了「四月」的欄位，這個查詢步驟依然有效)。
      * 如果您採用策略B，請選擇「取消資料行的樞紐 (Unpivot Columns)」。

   4. 重新命名：
      Power Query 會自動產生兩個新欄位，預設名稱為 屬性 (Attribute) 和 值 (Value)。請對這兩個新欄位的標題按兩下，將它們重新命名為有意義的名稱，例如「月份」和「銷售額」。

   5. 關閉並載入：
      點擊左上角的「關閉並載入」，將整理好的長表格載入到新的 Excel 工作表中。

4. 為什麼這個功能如此重要？

* 為了樞紐分析：只有「長表格」格式的資料，才能在樞紐分析表中發揮最大作用，讓您可以隨意拖拉「月份」或「產品」到欄、列或篩選區域。
* 為了自動化：這個轉換步驟會被 Power Query 記錄下來。下個月，當您的原始報表增加了「四月」的欄位時，您不需要重複任何操作，只需在查詢結果上按右鍵「重新整理」，四月的資料就會自動被逆樞紐並追加到長表格的末端。
* 為了標準化：它是將各種不規範、人類可讀的報表，轉換為標準化、機器可讀的「資料庫格式」的核心工具。



# AI 智能整合


## **認識你的新夥伴：AI 工具能做什麼？**

### **AI 工具是什麼？**

首先，讓我們先來認識一下今天的主角：生成式 AI。你可以把它想像成一個擁有海量知識、反應極快、而且 24 小時待命的超級大腦。它不僅能像朋友一樣跟你聊天，更能成為你工作上的得力助手。

### **AI 在數據分析中的三大角色**

在數據分析的領域，我們不該把 AI 看作一個取代者，而是一個「**智能副駕 (Co-pilot)**」。它能輔助我們完成許多任務，主要扮演以下三個關鍵角色：

1. **探索者 (Explorer)**
   當你面對一堆原始數據，還沒有頭緒時，AI 就是你最好的探索夥伴。它可以幫你腦力激盪，提出各種值得分析的商業問題。

   * **實作範例**：
     假設我們有一份銷售數據，欄位包含「日期、地區、產品、銷售額」。我們可以這樣問 AI：

     > 「我有一份銷售數據，欄位有『日期』、『地區』、『產品』、『銷售額』，我可以用這些數據回答哪些有趣的商業問題？」

2. **翻譯官 (Translator)**
   AI 能將我們用「人類語言」描述的複雜問題，精準地「翻譯」成 Excel 能聽懂的語言——也就是具體的分析步驟或函數公式。

3. **解讀者 (Interpreter)**
   當你從 Excel 得到分析結果（例如一張樞紐分析表）後，AI 可以幫助你快速解讀數據背後的趨勢與洞察，並生成初步的結論。

   * **實作範例**：
     我們可以將一張簡單的樞紐分析表結果貼給 AI，然後問：

     > 「根據這張表格，你看到了什麼趨勢？請用三句話總結。」

---

#### 怎麼開始

- [ChatGPT](https://chatgpt.com/)
- [perplexity](https://www.perplexity.ai/)
- [gemini](https://gemini.google.com/app?hl=zh-TW)
- [claude](https://claude.ai/)
- [grok](https://grok.com/?referrer=website)
- [Google Workspace Labs](https://workspace.google.com/labs-sign-up/u/0/)
- [安裝 gemini CLI](https://ithelp.ithome.com.tw/articles/10382348)

---

### Prompt 的基本概念與定義

#### 什麼是 Prompt？

Prompt (提示詞) 是給 AI 的一段輸入文字或指令，目的是引導模型產生特定的回應、完成某項任務，或進行特定的對話。

可以將 Prompt 想像成是對一位非常聰明但需要明確指示的助理所說的話。說什麼，以及如何說，將直接決定助理如何理解並給出回應。

#### Prompt 的核心目的

Prompt 的存在，是為了讓 AI 能夠：

1. 理解您的意圖：AI 需要知道你想讓它做什麼。
2. 設定情境：提供足夠的背景資訊，讓 AI 能夠在正確的脈絡下思考。
3. 定義輸出：規範 AI 回應的內容、格式、語氣、長度等。

---

####  一個好的 Prompt 應具備的要素

要從 AI 中獲得最佳、最符合預期的回應，你的 Prompt 應盡可能具備以下要素：

1. 清晰明確 (Clarity & Specificity)
   * 定義：避免模糊不清的詞語，直接說明你想執行的 Excel 計算或操作。AI 模型是字面意義上的理解者，**它不會「猜測」你的意思**。
   * 範例：
     * 不好：「給我一個公式。」（太籠統，AI 不知道要算什麼）
     * 好：「**請**給我一個 Excel 公式，計算 `A1` 到 `A10` 儲存格的總和。」（明確指出要計算的動作、範圍和目標）
2. 提供足夠的情境 (Context)
   * 定義：給予 AI 足夠的背景資訊，例如你的**資料結構、Excel 版本、特殊需求**等，讓它能更好地理解你的需求並選擇最適合的函數。
   * 範例：
     * 「我有一個員工資料表，其中 A 欄是員工編號，B 欄是部門，C 欄是月薪。現在我需要根據員工編號 (在**另一個工作表**的 A2 儲存格)，從這個資料表中查找對應的月薪。我的 Excel 版本是 Microsoft 365。 給我一個 Excel 公式。」（明確指出 Excel 版本和資料結構，影響 AI 選擇 VLOOKUP 而非 XLOOKUP）
3. 明確的指令 (Clear Instructions)
   * 定義：清楚地告訴 AI 你希望它執行什麼具體的 Excel 動作（例如：計算、查找、篩選、格式化）。
   * 範例：
     * 「計算 A1 到 A10 儲存格的平均值。」
     * 「查找 B2 儲存格的產品名稱在 產品清單 工作表中的對應價格。」
     * 「判斷 C5 儲存格的數值是否大於 100。」
4. 指定輸出格式 (Desired Format)
   * 定義：如果你對 AI 回傳的公式或其解釋有特定的格式要求，明確說明。
   * 範例：
     * 「給我一個 Excel 公式，計算 A1 到 A10 的總和。將公式用程式碼區塊呈現，並附上簡要的中文解釋。」
     * 「給我一個 Excel 公式，計算 A1 到 A10 的總和。以條列式說明公式的每個參數。」
5. 設定限制 (Constraints)
   * 定義：限制公式的行為或其結果的特性，例如錯誤處理、特定條件下的回傳值等。
   * 範例：
     * 「給我一個 Excel 公式，計算 B2 儲存格的銷售額減去 C2 儲存格的成本。**確保公式在銷售額為零時不會產生錯誤，並回傳 0。**」（限制了公式的錯誤處理行為）
     * 「給我一個 Excel 公式，判斷 A1 儲存格的數字是奇數還是偶數。**如果 `A1` 為空，回傳空白。**」（限制了公式在特定條件下的回傳值）
6. 提供範例 (Few-shot Examples)
   * 定義：如果你的需求比較複雜、抽象，或者你希望 AI 遵循特定的公式模式，提供一兩個輸入-輸出範例可以幫助 AI 更好地理解你的意圖。
   * 範例：
     * 「根據以下範例，為我生成 Excel 公式：
       * 範例 1：
         * 需求：計算 A1 到 A5 的總和。
         * 公式：=SUM(A1:A5)
       * 範例 2：
         * 需求：計算 B1 到 B10 的平均值。
         * 公式：=AVERAGE(B1:B10)
       * 現在請生成：
         * 需求：找出 C1 到 C20 的最大值。」（透過範例，AI 能理解你期望的公式生成模式）
7. 「忘記一切」的重設指令（Forget everything)
   * 定義：強制 AI 清除錯誤記憶與假設，強制重啟對話，解決 AI 陷入重複或迷失情境的問題。
   * 範例指令：「忘記從 [對話中的特定時間點] 開始的一切。重新開始並逐步解決這個問題。」
8. 啟動導師模式(like a teacher)
   * 定義：強迫 AI 在給出最終答案前，像是一位老師以淺顯易懂的方式逐步解釋推理過程，協助提高結果的準確性，讓使用者得以學習 AI 的思考邏輯。
   * 範例：「在給出最終答案前，請逐步解釋你的思考過程。」

---

> 實作練習：

1. 請打開你慣用的 AI 工具網頁（如 ChatGPT, Gemini 等）。

2. **任務**：假設你正在分析客戶意見，請複製以下這段模擬的「客戶回饋文字」，並向 AI 提問。

   > **回饋文字**：「你們的App更新後變得很卡，而且常常閃退。雖然客服人員態度很好，但問題等了好幾天才解決。希望你們能改善穩定性，並加快問題處理速度。」
   >
   > **你的提問**：「請將這份客戶回饋進行分類，並總結出 3 個主要的客戶抱怨點。」

3. 觀察 AI 如何從非結構化的文字中，快速提煉出有價值的資訊。

#### 簡述提示五大原則

- **給予方向**
  詳細描述所需的風格,或參考某個相關人物。
- **指定格式**
  定義需要遵循的規則和指定的回應結構。
- **提供範例**
  插入一組可正確完成任務的測試案例。
- **評估品質**
  辨識錯誤並對回應評分,測試有哪些會影響效能的因素。
- **任務分工**
  將任務分為多個步驟,再鏈接起來實現複雜的目標。

---



## 讓 AI 成為你的 Excel 超級助理

認識了 AI 的角色後，接下來我們要學習如何讓它在 Excel 的日常工作中助我們一臂之力。

### **1. 函數公式的解讀與生成**

你是否曾經被複雜的 `INDEX` + `MATCH` 公式搞得頭昏眼花？或是想了半天也想不出 `SUMIFS` 的條件該怎麼下？現在，你有了更好的解決方案！

* **看不懂？問它！**：直接把看不懂的公式貼給 AI，請它用白話文解釋給你聽。

* **寫不出？叫它寫！**：用自然語言描述你的需求，AI 就能為你生成對應的公式。

  * **講師示範**：

    > 「我有兩張表，A 表是『訂單列表』(有產品ID)，B 表是『產品資訊』(有產品ID、產品名稱、價格)。請給我一個 Excel 公式，讓我在 A 表中根據『產品ID』查找到對應的『價格』。」

### **2. 自動化數據清理**

面對從系統導出的髒數據，例如格式混亂的地址、非標準的日期，過去我們可能要花費大量時間手動清理。現在，你可以透過向 AI 描述「處理前」與「處理後」的狀態，來快速獲得解決方案。

*   **示範**：

> 「我的 A 欄有一個地址列表，格式為『台灣台北市信義區市府路1號』，我只想在 B 欄提取出『台北市』，請問公式怎麼寫？」

```excel
=LEFT(TEXTAFTER(A1, "台灣"), MIN(FIND({"區","鄉","鎮","市"}, TEXTAFTER(A1, "台灣")&"區鄉鎮市"))-1)
```

> 少了市，修正錯誤

```excel
=LEFT(TEXTAFTER(A1, "台灣"), MIN(FIND({"市","縣"}, TEXTAFTER(A1, "台灣")&"市縣")))
```

> 實作練習：取出地址市區以外部分，如市府路1號

```excel
=TEXTAFTER(TEXTAFTER(G10, "市"), "區")
```

### **3. 如何問出好問題？**

要讓 AI 給出最精準的答案，關鍵在於「提問的品質」。請記住這個黃金框架，你的問題越清晰，AI 的回答就越到位：

1.  **背景 (Context)**：你在做什麼？目的是什麼？
2.  **目標 (Objective)**：你希望達成什麼具體的結果？
3.  **資料結構 (Data Structure)**：你的資料長什麼樣子？（最好提供欄位名稱和幾行範例）
4.  **預期輸出 (Expected Output)**：你希望 AI 給你的答案是什麼格式？（一個公式？還是一個步驟說明？）
5.  **環境 (Environment)**：你使用的 Excel 版本是什麼？（這會影響 AI 是否推薦 365 版的專屬新函數）

---

> 實作練習：

現在，讓我們打開練習檔案，實際操作看看！

1.  請打開我們提供的 `課堂練習.xlsx`。
2.  **任務一：公式生成**
    *   請你向 AI 提問，目標是將「農產品交易紀錄」中當天每個市場的總交易量的「市場的交易量」。
3.  **任務二：日期格式轉換**
    *   在員工資料表中，新增一欄「離職日期」的格式是文字（例如 `2023-Jan-15`）。
    *   向 AI 提問，找到一個最簡單的方法，將這一欄的文字轉換為 Excel 可辨識、可計算的標準日期格式。


農產品交易行情工作表的欄位F為市場名稱，欄位K為交易量，欄位D為作物名稱，依不同市場名稱將不同作物的交易量進行加總，結果輸出依序為市場名稱，作物名稱，總成交金額

```excel
=LET(
    市場作物, UNIQUE(CHOOSE({1,2}, 農產品交易行情!F:F, 農產品交易行情!D:D)),
    市場, INDEX(市場作物,,1),
    作物, INDEX(市場作物,,2),
    交易量, SUMIFS(農產品交易行情!K:K, 農產品交易行情!F:F, 市場, 農產品交易行情!D:D, 作物),
    HSTACK(市場, 作物, 交易量)
)
```



## 從數據到報告：一個完整的 AI 協作流程

學會了單點的技巧後，我們要將它們串連起來，形成一個高效的工作流程。

### **標準 AI 協作工作流**

1.  **探索 (AI)**：針對你的原始數據，向 AI 提問：「我該從哪些角度分析？」
2.  **執行 (Excel)**：根據 AI 的建議，回到 Excel 中，利用你學過的樞紐分析等工具，快速完成數據的計算與分析。
3.  **解讀 (AI)**：將 Excel 的分析結果（例如，複製樞紐分析表的內容）貼回給 AI，並下指令：「請幫我解讀這份數據，並生成文字摘要與結論。」
4.  **呈現 (Excel)**：將 AI 生成的精闢摘要，稍作修飾後，放入你的 Excel 報告或儀表板中，讓你的報告不僅有數據，更有洞見！

### **優化你的報告**

你甚至可以請 AI 幫你優化報告的呈現方式！

* **圖表美化**：截圖你的圖表，詢問 AI：「如何讓這張圖表更具可讀性？配色或標題有什麼建議？」

* **生成摘要**：這是本單元的重點。學習如何將「量化結果」（表格）與「質化需求」（報告目標）結合，讓 AI 產出符合商業情境的分析短評。

  * **示範**：

    > （貼上按渠道分類的瀏覽量樞紐分析表後）
    > 「你是一位資深的行銷分析師，這是我們今年的的銷售數據。請為我的行銷年報寫一段 100 字的摘要，並明確指出哪個渠道表現最好，哪個最需要我們關注。」

---

> 實作練習：jobs104.xlsx

1. **步驟一 (探索)**：向 AI 提問，從這份數據中，找出 2-3 個值得分析的維度（例如：薪資競爭力與價值分析？關鍵人才供需熱點 ？）。

2. **步驟二 (執行)**：在 Excel 中，根據 AI 的建議，自己動手建立對應的樞紐分析表。

3. **步驟三 (解讀)**：將樞紐分析表的結果複製貼回 ChatGPT，並下達這個指令：

   > 「你是一位頂尖的數據分析師，請根據這份分析數據，為我的主管寫一段簡短的摘要，內容需包含關鍵發現與未來建議。」


   >1. 完成上傳檔案，prompt 輸入根據這份數據提供最重要的三個維度分析建議
   >2. 薪資競爭力與價值分析？關鍵人才供需熱點 ？如何建立excel樞紐分析表